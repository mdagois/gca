include "memory.rgbinc"
include "utils.rgbinc"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

export PrinterInitialize, PrinterUpdate

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "printer_initialize", rom0

LoadTileset:
	ld hl, tileset_data
	ld de, _VRAM
	.copy
		ld a, [hli]
		ld [de], a
		inc de

		ld a, l
		cp a, low(tileset_data_end)
		jr nz, .copy

		ld a, h
		cp a, high(tileset_data_end)
		jr nz, .copy
	ret

PrinterInitialize:
	ClearMemory _RAM, WRAM_END
	ClearMemory _HRAM, HRAM_END
	InitPadInput vPadInput
	call ProtocolInitialize

	; init graphics
	copy [rBGP], %11100100
	call LoadTileset

	; set a tilemap to display the image data
	xor a
	ld hl, _SCRN0
	ld de, SCRN_VX_B - SCRN_X_B
	ld c, SCRN_Y_B
	.set_line
rept SCRN_X_B
		ld [hli], a
		inc a
endr
		add hl, de
		dec c
		jr nz, .set_line

	; enable interrupts
	copy [rIE], IEF_VBLANK | IEF_STAT | IEF_SERIAL
	copy [rSTAT], STATF_LYC
	copy [rLYC], 12 * 8
	ei

	; set the graphics parameters and turn back LCD on
	copy [rLCDC], LCDCF_ON | LCDCF_BG9800 | LCDCF_BGON

	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "printer_update", rom0

PrinterUpdate:
	ld hl, vIsVblank
	xor a
	.wait_vblank
		halt
		cp a, [hl]
		jr z, .wait_vblank
		ld [hl], a

	; switch to tile addressing mode 8000 from line 0
	; this displayed tiles 0 to 239
	ld a, [rLCDC]
	set LCDCB_BG8000, a
	ld [rLCDC], a

	call ProtocolUpdateVblank

	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	UpdatePadInput vPadInput
	call ProtocolUpdate

	ret

