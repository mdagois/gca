include "data.rgbinc"
include "memory.rgbinc"

export _Game_Initialize, _Game_Update, _Game_UpdateGraphics

section "game", rom0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

def REAL_FRACTION_PART	equ (0)
def REAL_INTEGER_PART	equ (1)

; set the value of a 16-bit integer
macro Set16
	ld a, low(\2)
	ldh [\1 + REAL_FRACTION_PART], a
	ld a, high(\2)
	ldh [\1 + REAL_INTEGER_PART], a
endm

; perform a 16-bit add at addresses \1 and \2
; the result is stored back at address \1
macro Add16
	ldh a, [\1 + REAL_FRACTION_PART]
	ld b, a
	ldh a, [\2 + REAL_FRACTION_PART]
	add a, b
	ldh [\1 + REAL_FRACTION_PART], a

	ldh a, [\1 + REAL_INTEGER_PART]
	ld b, a
	ldh a, [\2 + REAL_INTEGER_PART]
	adc a, b
	ldh [\1 + REAL_INTEGER_PART], a
endm

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

ResetScore:
	xor a
	ldh [vScore + 0], a
	ldh [vScore + 1], a
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

def BALL_INITIAL_SPEED		equ (1)
def BALL_INITIAL_DIR_X		equ ($0080)
def BALL_INITIAL_DIR_Y		equ ($0080)

_Game_Initialize:
	call ResetScore

	; set sprite tile IDs
	copy [vBallOam + OAMA_TILEID], TILE_ID_BALL
	ld a, TILE_ID_PADS
	ld [vLeftPadTopOam + OAMA_TILEID], a
	inc a
	ld [vLeftPadBottomOam + OAMA_TILEID], a
	inc a
	ld [vRightPadTopOam + OAMA_TILEID], a
	inc a
	ld [vRightPadBottomOam + OAMA_TILEID], a

	; set ball sprite initial positions
	ld a, (SCRN_X + OAM_X_OFS) / 2 + 2
	ldh [vBallX + REAL_INTEGER_PART], a
	ld a, (SCRN_Y + OAM_Y_OFS) / 2 - 2
	ldh [vBallY + REAL_INTEGER_PART], a

	; set pad fixed position
	ld a, OAM_X_OFS
	ld [vLeftPadTopOam + OAMA_X], a
	ld [vLeftPadBottomOam + OAMA_X], a
	ld a, SCRN_X
	ld [vRightPadTopOam + OAMA_X], a
	ld [vRightPadBottomOam + OAMA_X], a

	; set pad variable positions
	ld a, ((SCRN_Y + OAM_Y_OFS) / 2) - 8
	ldh [vLeftPadY], a
	ldh [vRightPadY], a

	; set initial ball dynamics
	Set16 vBallDirectionX, BALL_INITIAL_DIR_X
	Set16 vBallDirectionY, BALL_INITIAL_DIR_Y
	ld a, BALL_INITIAL_SPEED
	ldh [vBallSpeed], a

	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

def WALL_HEIGHT			equ (8)
def PAD_HEIGHT			equ (16)
def BALL_HEIGHT			equ (4)

def PAD_MIN_Y			equ (OAM_Y_OFS + WALL_HEIGHT)
def PAD_MAX_Y			equ (SCRN_Y + OAM_Y_OFS - WALL_HEIGHT - PAD_HEIGHT)

def BALL_MIN_Y			equ (OAM_Y_OFS + WALL_HEIGHT)
def BALL_MAX_Y			equ (SCRN_Y + OAM_Y_OFS - WALL_HEIGHT - BALL_HEIGHT)

_Game_Update:
	; move and apply constraint to left pad
	PadInput_TestHeld vPadInput, PADF_UP
	jr nz, .left_up
		ldh a, [vLeftPadY]
		dec a
		cp a, PAD_MIN_Y
		jr nc, .left_underflow
			ld a, PAD_MIN_Y
		.left_underflow
		ldh [vLeftPadY], a
	.left_up
	PadInput_TestHeld vPadInput, PADF_DOWN
	jr nz, .left_down
		ldh a, [vLeftPadY]
		inc a
		cp a, PAD_MAX_Y
		jr c, .left_overflow
			ld a, PAD_MAX_Y
		.left_overflow
		ldh [vLeftPadY], a
	.left_down

if def(DEBUG_CONTROL_RIGHT_PAD)
	; move and apply constraint to right pad
	PadInput_TestHeld vPadInput, PADF_LEFT
	jr nz, .right_up
		ldh a, [vRightPadY]
		dec a
		cp a, PAD_MIN_Y
		jr nc, .right_underflow
			ld a, PAD_MIN_Y
		.right_underflow
		ldh [vRightPadY], a
	.right_up
	PadInput_TestHeld vPadInput, PADF_RIGHT
	jr nz, .right_down
		ldh a, [vRightPadY]
		inc a
		cp a, PAD_MAX_Y
		jr c, .right_overflow
			ld a, PAD_MAX_Y
		.right_overflow
		ldh [vRightPadY], a
	.right_down
endc

	; update ball
	ldh a, [vBallSpeed]
	ld c, a
	.ball_update
		Add16 vBallX, vBallDirectionX
		Add16 vBallY, vBallDirectionY
		; TODO Collision detection
		; TODO Collision response
		; TODO Update score
		dec c
		jr nz, .ball_update

	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; update the game graphics
; must be used during vblank
_Game_UpdateGraphics:
	ldh a, [vBallX + REAL_INTEGER_PART]
	ld [vBallOam + OAMA_X], a
	ldh a, [vBallY + REAL_INTEGER_PART]
	ld [vBallOam + OAMA_Y], a

	ldh a, [vLeftPadY]
	ld [vLeftPadTopOam + OAMA_Y], a
	add a, 8
	ld [vLeftPadBottomOam + OAMA_Y], a

	ldh a, [vRightPadY]
	ld [vRightPadTopOam + OAMA_Y], a
	add a, 8
	ld [vRightPadBottomOam + OAMA_Y], a

	ret

